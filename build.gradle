buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:0.4.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.1.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.28.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.68.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-circleci'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    // Duplicate baseline defaults, excluding the release plugin
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-format'
    apply plugin: 'com.palantir.baseline-class-uniqueness'
    apply plugin: 'com.palantir.baseline-reproducibility'
    apply plugin: 'com.palantir.baseline-exact-dependencies'
    apply plugin: 'com.palantir.baseline-encoding'
    apply plugin: 'com.palantir.baseline-testing'

    apply plugin: 'com.palantir.java-format'
    version rootProject.version
    group 'com.palantir.jvm.diagnostics'

    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    tasks.checkExplicitSourceCompatibility.enabled = false

    tasks.check.dependsOn(javadoc)

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
}
